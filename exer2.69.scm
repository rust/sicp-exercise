(define (make-leaf symbol weight)
  (list 'leaf symbol weight))
(define (leaf? object)
  (eq? (car object) 'leaf))
(define (symbol-leaf x) (cadr x))
(define (weight-leaf x) (caddr x))
(define (make-code-tree left right)
  (list left
        right
        (append (symbols left) (symbols right))
        (+ (weight left) (weight right))))

(define (left-branch tree) (car tree))
(define (right-branch tree) (cadr tree))
(define (symbols tree)
  (if (leaf? tree)
      (list (symbol-leaf tree))
      (caddr tree)))
(define (weight tree)
  (if (leaf? tree)
      (weight-leaf tree)
      (cadddr tree)))
(define (adjoin-set x set)
  (cond ((null? set) (list x))
        ((< (weight x) (weight (car set))) (cons x set))
        (else (cons (car set)
                    (adjoin-set x (cdr set))))))
(define (generate-huffman-tree pairs)
  (successive-merge (make-leaf-set pairs)))
(define (make-leaf-set pairs)
  (if (null? pairs)
      '()
      (let ((pair (car pairs)))
        (adjoin-set (make-leaf (car pair)
                               (cadr pair))
                    (make-leaf-set (cdr pairs))))))

(define (successive-merge ordered-pairs)
  (if (null? (cdr ordered-pairs))
      (car ordered-pairs)
      (successive-merge
       (adjoin-set
        (make-code-tree (car ordered-pairs)
                        (cadr ordered-pairs))
        (cddr ordered-pairs)))))
(define sample-ordered-pairs
  (make-leaf-set (list '(A 4) '(B 2) '(C 1) '(D 1) '(E 1) '(F 1))))
(print (successive-merge sample-ordered-pairs))
;; => (((leaf B 2) ((leaf F 1) (leaf E 1) (F E) 2) (B F E) 4) (((leaf D 1) (leaf C 1) (D C) 2) (leaf A 4) (D C A) 6) (B F E D C A) 10)
